## Shadow Quality

既然我们拥有了功能完善的级联阴影贴图，让我们专注于提高阴影的质量。我们一直观察到的伪影被称为阴影痤疮，它是由于那些与光线方向不完全对齐的表面错误的自阴影造成的。随着表面越来越接近与光线方向平行，痤疮问题会变得更严重。

增加纹理集大小会减小纹素的世界空间大小，从而使痤疮伪影变小。然而，伪影的数量也会增加，所以问题不能简单地通过增加纹理集大小来解决。

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/shadow-acne.png)

### Depth Bias

有各种方法可以减轻阴影痤疮的问题。其中最简单的方法是在投射阴影的物体的深度上添加一个恒定的偏移，将它们从光线远离，以便不再发生错误的自阴影。最快的方法是在渲染时应用全局深度偏移，在DrawShadows之前在缓冲区上调用SetGlobalDepthBias，并在之后将其设置为零。这是一个在裁剪空间中应用的深度偏移，是一个非常小的值的倍数，具体取决于用于阴影贴图的确切格式。我们可以通过使用一个大值（如50000）来了解它的工作原理。还有第二个参数用于斜率缩放偏移，但暂时将其保持为零。

```
			buffer.SetGlobalDepthBias(50000f, 0f);
			ExecuteBuffer();
			context.DrawShadows(ref shadowSettings);
			buffer.SetGlobalDepthBias(0f, 0f);
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/constant-depth-bias.png)

恒定的偏移确实简单，但只能够消除大部分正面照明的表面的伪影。要消除所有的痤疮，需要一个更大的偏移，比原来大一个数量级。

```
		buffer.SetGlobalDepthBias(500000f, 0f);
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/large-depth-bias.png)

然而，由于深度偏移将投射阴影的物体远离光线，采样到的阴影也会朝着相同的方向移动。足够大以消除大部分痤疮的偏移会将阴影移动得太远，以至于它们看起来与其投射物分离，导致被称为“彼得潘现象”的视觉伪影。

![without](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/without-peter-panning.png)

![with](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/with-peter-panning.png)

另一种方法是应用斜率缩放偏移，这可以通过在SetGlobalDepthBias的第二个参数中使用非零值来实现。该值用于缩放X和Y维度上绝对裁剪空间深度导数的最大值。因此，对于正面照明的表面，这个值为零；当光线以至少在两个维度中的一个维度上以45°角射击时，它为1；当表面法线与光线方向的点积达到零时，它趋近于无穷大。因此，当需要更多偏移时，偏移会自动增加，但没有上限。结果是，为了消除痤疮，需要一个更低的因子，例如3，而不是500000。

```
buffer.SetGlobalDepthBias(0f, 3f);
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/slope-scale-bias.png)



### Cascade Data

因为痤疮的大小取决于世界空间纹素大小，一个适用于所有情况的一致性方法必须考虑到这一点。由于每个级联的纹素大小都不同，这意味着我们需要将一些更多的级联数据发送到GPU。为此，在Shadows中添加一个通用的级联数据向量数组。

```
	static int
		…
		cascadeCullingSpheresId = Shader.PropertyToID("_CascadeCullingSpheres"),
		cascadeDataId = Shader.PropertyToID("_CascadeData"),
		shadowDistanceFadeId = Shader.PropertyToID("_ShadowDistanceFade");

	static Vector4[]
		cascadeCullingSpheres = new Vector4[maxCascades],
		cascadeData = new Vector4[maxCascades];
```

将它与其他所有内容一起发送到GPU。

```
		buffer.SetGlobalVectorArray(
			cascadeCullingSpheresId, cascadeCullingSpheres
		);
		buffer.SetGlobalVectorArray(cascadeDataId, cascadeData);
```

我们已经可以做的一件事是将级联半径的平方的倒数放入这些向量的X分量中。这样，我们就不必在着色器中执行这个除法操作。在一个新的SetCascadeData方法中完成这个步骤，同时存储剔除球，并在RenderDirectionalShadows中调用它。将级联索引、剔除球和瓦片大小作为浮点数传递给它。

```
	void RenderDirectionalShadows (int index, int split, int tileSize) {
		…
		
		for (int i = 0; i < cascadeCount; i++) {
			…
			if (index == 0) {
				SetCascadeData(i, splitData.cullingSphere, tileSize);
			}
			…
		}
	}

	void SetCascadeData (int index, Vector4 cullingSphere, float tileSize) {
		cascadeData[index].x = 1f / cullingSphere.w;
		cullingSphere.w *= cullingSphere.w;
		cascadeCullingSpheres[index] = cullingSphere;
	}
```

```
CBUFFER_START(_CustomShadows)
	int _CascadeCount;
	float4 _CascadeCullingSpheres[MAX_CASCADE_COUNT];
	float4 _CascadeData[MAX_CASCADE_COUNT];
	…
CBUFFER_END
```

```
				data.strength *= FadedShadowStrength(
					distanceSqr, _CascadeData[i].x, _ShadowDistanceFade.z
				);
```

### Normal Bias

不正确的自阴影发生是因为一个投射阴影的深度纹素覆盖了多个片段，这导致投射物的体积从其表面伸出。因此，如果我们缩小投射物，这种情况就不会再发生。然而，缩小投射物会使阴影变得比它们应该的更小，并且可能会引入不应该存在的空洞。

我们也可以做相反的操作：在采样阴影时膨胀表面。然后，我们在表面上采样时稍微远离表面，刚好足够避免错误的自阴影。这将稍微调整阴影的位置，可能会导致边缘处的不对齐，并添加虚假的阴影，但这些伪影通常远不如彼得潘现象明显。

我们可以通过沿着法线矢量稍微移动表面位置来采样阴影。如果我们只考虑一个维度，那么与世界空间纹素大小相等的偏移量应该足够了。我们可以在SetCascadeData中通过将剔除球的直径除以瓦片大小来找到纹素大小。将其存储在级联数据向量的Y分量中。

```
		float texelSize = 2f * cullingSphere.w / tileSize;
		cullingSphere.w *= cullingSphere.w;
		cascadeCullingSpheres[index] = cullingSphere;
		//cascadeData[index].x = 1f / cullingSphere.w;
		cascadeData[index] = new Vector4(
			1f / cullingSphere.w,
			texelSize *  * 1.4142136f
		);
```

然而，这并不总是足够，因为纹素是正方形的。在最坏的情况下，我们可能需要沿着正方形的对角线进行偏移，所以让我们乘以√2来进行缩放。

在着色器方面，对GetDirectionalShadowAttenuation添加一个全局阴影数据的参数。将表面法线与偏移相乘，以找到法线偏移，并在计算在阴影瓦片空间中的位置之前将其添加到世界位置中。

```
float GetDirectionalShadowAttenuation (
	DirectionalShadowData directional, ShadowData global, Surface surfaceWS
) {
	if (directional.strength <= 0.0) {
		return 1.0;
	}
	float3 normalBias = surfaceWS.normal * _CascadeData[global.cascadeIndex].y;
	float3 positionSTS = mul(
		_DirectionalShadowMatrices[directional.tileIndex],
		float4(surfaceWS.position + normalBias, 1.0)
	).xyz;
	float shadow = SampleDirectionalShadowAtlas(positionSTS);
	return lerp(1.0, shadow, directional.strength);
}
```

![sphere](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/normal-bias-sphere.png)

### Configurable Biases

法线偏移可以消除阴影痤疮，而不会引入明显的新伪影，但它无法消除所有的阴影问题。例如，墙下的地板上可见到不应该存在的阴影线。这不是自阴影，而是从墙壁中伸出影响其下方地板的阴影。稍微添加一些斜率缩放偏移可以处理这些情况，但是没有一个完美的值可供使用。因此，我们将根据每个光源进行配置，使用其现有的偏移滑块。在Shadows的ShadowedDirectionalLight结构中为其添加一个字段。

```
		public int visibleLightIndex;
		public float slopeScaleBias;
	}
```

光的偏移通过其 shadowBias 属性提供。将其添加到 ReserveDirectionalShadows 中的数据中。

```
			shadowedDirectionalLights[ShadowedDirectionalLightCount] =
				new ShadowedDirectionalLight {
					visibleLightIndex = visibleLightIndex,
					slopeScaleBias = light.shadowBias
				};
```

And use it to configure the slope-scale bias in `RenderDirectionalShadows`.

```
			buffer.SetGlobalDepthBias(0f, light.slopeScaleBias);
			ExecuteBuffer();
			context.DrawShadows(ref shadowSettings);
			buffer.SetGlobalDepthBias(0f, 0f);
```

让我们还使用光的现有法线偏移滑块来调制我们应用的法线偏移。让 ReserveDirectionalShadows 返回一个 Vector3，并使用光的 shadowNormalBias 作为新的 Z 分量。

```
	public Vector3 ReserveDirectionalShadows (
		Light light, int visibleLightIndex
	) {
		if (…) {
			…
			return new Vector3(
				light.shadowStrength,
				settings.directional.cascadeCount * ShadowedDirectionalLightCount++,
				light.shadowNormalBias
			);
		}
		return Vector3.zero;
	}
```

将新的法线偏移添加到 DirectionalShadowData 中，并在 Shadows 中的 GetDirectionalShadowAttenuation 中应用它。

```
struct DirectionalShadowData {
	float strength;
	int tileIndex;
	float normalBias;
};

…

float GetDirectionalShadowAttenuation (…) {
	…
	float3 normalBias = surfaceWS.normal *
		(directional.normalBias * _CascadeData[global.cascadeIndex].y);
	…
}
```

And configure it in `GetDirectionalShadowData` in *Light*.

```
	data.tileIndex =
		_DirectionalLightShadowData[lightIndex].y + shadowData.cascadeIndex;
	data.normalBias = _DirectionalLightShadowData[lightIndex].z;
```

现在我们可以根据每个光源进行调整这两个偏移值。将斜坡比例偏移设为零，法线偏移设为1是一个不错的默认值。如果您增加第一个值，您可以减小第二个值。但请记住，我们对这些光设置进行了与其原始目的不同的解释。它们曾经是剪切空间深度偏移和世界空间法线偏移。因此，当您创建一个新的光源时，您可能会遇到严重的“Peter-Panning”效应，直到您调整这些偏移值。

![settings](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/light-settings.png)

![sphere](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/configured-bias-sphere.png)



### Shadow Pancaking

另一个可能引发伪影的潜在问题是Unity应用了阴影平铺技术。其思想是，在渲染方向光的投射阴影时，近裁剪面会尽量向前移动。这可以增加深度精度，但这意味着那些不在摄像机视图内的投射阴影物体可能会出现在近裁剪面的前面，导致它们在不应该被裁剪的情况下被剪切掉。

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/clipped-shadows.png)

这个问题可以通过在ShadowCasterPassVertex中将顶点位置限制在近裁剪面上来解决，从而有效地将那些位于近裁剪面前方的投射阴影物体扁平化，将它们变成粘附在近裁剪面上的薄饼状。我们可以通过取裁剪空间Z和W坐标的最大值来实现这一点，或者在定义了UNITY_REVERSED_Z时取它们的最小值。为了使用正确的W坐标符号，将其与UNITY_NEAR_CLIP_VALUE相乘。

```
	output.positionCS = TransformWorldToHClip(positionWS);

	#if UNITY_REVERSED_Z
		output.positionCS.z =
			min(output.positionCS.z, output.positionCS.w * UNITY_NEAR_CLIP_VALUE);
	#else
		output.positionCS.z =
			max(output.positionCS.z, output.positionCS.w * UNITY_NEAR_CLIP_VALUE);
	#endif
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/clamped-shadows.png)

这对于完全位于近裁剪面两侧的投射阴影物体效果非常好，但是横跨裁剪面的投射阴影物体会变形，因为只有其中的一些顶点受到影响。这对于小三角形来说不太明显，但是大三角形可能会发生很大的变形，弯曲它们并经常导致它们陷入表面中。

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/deformed-shadows.png)

这个问题可以通过稍微拉回近裁剪面来缓解。这就是灯光的近裁剪面滑块的作用。在ShadowedDirectionalLight中为近裁剪面偏移添加一个字段。

```
	struct ShadowedDirectionalLight {
		public int visibleLightIndex;
		public float slopeScaleBias;
		public float nearPlaneOffset;
	}
```

And copy the light's `shadowNearPlane` property to it.

```
			shadowedDirectionalLights[ShadowedDirectionalLightCount] =
				new ShadowedDirectionalLight {
					visibleLightIndex = visibleLightIndex,
					slopeScaleBias = light.shadowBias,
					nearPlaneOffset = light.shadowNearPlane
				};
```

我们通过填写 ComputeDirectionalShadowMatricesAndCullingPrimitives 的最后一个参数来应用它，该参数我们仍然给定一个固定值为零。

```
			cullingResults.ComputeDirectionalShadowMatricesAndCullingPrimitives(
				light.visibleLightIndex, i, cascadeCount, ratios, tileSize,
				light.nearPlaneOffset, out Matrix4x4 viewMatrix,
				out Matrix4x4 projectionMatrix, out ShadowSplitData splitData
			);
```

### PCF Filtering


到目前为止，我们只使用硬阴影，通过对每个片段进行一次阴影贴图采样。阴影比较采样器使用一种特殊形式的双线性插值，在插值之前执行深度比较。这被称为百分比近过滤（PCF），具体来说是 2×2 PCF 滤波，因为涉及到四个纹素。

但这不是我们过滤阴影贴图的唯一方式。我们还可以使用更大的滤波器，使阴影更加柔和，减少走样，尽管准确性较低。让我们添加对 2×2、3×3、5×5 和 7×7 滤波的支持。我们不会使用现有的软阴影模式来控制每个光源的这个选项。相反，我们将使所有定向光源使用相同的滤波器。在 ShadowSettings 中为此添加一个 FilterMode 枚举，以及在 Directional 中设置默认为 2×2 的 filter 选项。

```
	public enum FilterMode {
		PCF2x2, PCF3x3, PCF5x5, PCF7x7
	}

	…

	[System.Serializable]
	public struct Directional {

		public MapSize atlasSize;

		public FilterMode filter;

		…
	}

	public Directional directional = new Directional {
		atlasSize = MapSize._1024,
		filter = FilterMode.PCF2x2,
		…
	};
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/filter.png)

我们将为新的滤波模式创建着色器变种。在 Shadows 中添加一个包含三个关键字的静态数组来实现这一点。

```
	static string[] directionalFilterKeywords = {
		"_DIRECTIONAL_PCF3",
		"_DIRECTIONAL_PCF5",
		"_DIRECTIONAL_PCF7",
	};
```

创建一个 SetKeywords 方法，该方法可以启用或禁用适当的关键字。在执行缓冲区之前，在 RenderDirectionalShadows 中调用该方法。

```
	void RenderDirectionalShadows () {
		…
		SetKeywords();
		buffer.EndSample(bufferName);
		ExecuteBuffer();
	}

	void SetKeywords () {
		int enabledIndex = (int)settings.directional.filter - 1;
		for (int i = 0; i < directionalFilterKeywords.Length; i++) {
			if (i == enabledIndex) {
				buffer.EnableShaderKeyword(directionalFilterKeywords[i]);
			}
			else {
				buffer.DisableShaderKeyword(directionalFilterKeywords[i]);
			}
		}
	}
```

更大的滤波器需要更多的纹理采样。为了在着色器中进行这些计算，我们需要知道贴图集的大小和纹素大小。为此，在着色器中添加一个用于标识这些数据的着色器标识符。

```
		cascadeDataId = Shader.PropertyToID("_CascadeData"),
		shadowAtlasSizeId = Shader.PropertyToID("_ShadowAtlasSize"),
		shadowDistanceFadeId = Shader.PropertyToID("_ShadowDistanceFade");
```

And add it to `_CustomShadow` on the shader side.

```
CBUFFER_START(_CustomShadows)
	…
	float4 _ShadowAtlasSize;
	float4 _ShadowDistanceFade;
CBUFFER_END
```

Store the size in its X component and texel size in its Y component.

```
		SetKeywords();
		buffer.SetGlobalVector(
			shadowAtlasSizeId, new Vector4(atlasSize, 1f / atlasSize)
		);
```

在 Lit 的 CustomLit 通道中，使用 #pragma multi_compile 指令为三个关键字添加编译选项，另外还要为不使用关键字的 2×2 滤波选项添加匹配项。

```
			#pragma shader_feature _PREMULTIPLY_ALPHA
			#pragma multi_compile _ _DIRECTIONAL_PCF3 _DIRECTIONAL_PCF5 _DIRECTIONAL_PCF7
			#pragma multi_compile_instancing
```

我们将使用核心 RP 库中的 Shadow/ShadowSamplingTent HLSL 文件中定义的函数，因此请将其包含在 Shadows 的顶部。如果定义了 3×3 关键字，我们需要总共四个滤波采样，这将通过 SampleShadow_ComputeSamples_Tent_3x3 函数进行设置。我们只需要进行四次采样，因为每次采样都使用双线性 2×2 滤波。这四个采样覆盖了一个以半个纹素为偏移的正方形，涵盖了 3×3 个纹素，使用帐篷滤波器，中心的权重比边缘更强。

```
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Shadow/ShadowSamplingTent.hlsl"

#if defined(_DIRECTIONAL_PCF3)
	#define DIRECTIONAL_FILTER_SAMPLES 4
	#define DIRECTIONAL_FILTER_SETUP SampleShadow_ComputeSamples_Tent_3x3
#endif

#define MAX_SHADOWED_DIRECTIONAL_LIGHT_COUNT 4
#define MAX_CASCADE_COUNT 4
```

出于同样的原因，我们可以使用 9 个采样点来实现 5×5 滤波，以及 16 个采样点来实现 7×7 滤波，还需要相应命名的函数来处理它们。

```
#if defined(_DIRECTIONAL_PCF3)
	#define DIRECTIONAL_FILTER_SAMPLES 4
	#define DIRECTIONAL_FILTER_SETUP SampleShadow_ComputeSamples_Tent_3x3
#elif defined(_DIRECTIONAL_PCF5)
	#define DIRECTIONAL_FILTER_SAMPLES 9
	#define DIRECTIONAL_FILTER_SETUP SampleShadow_ComputeSamples_Tent_5x5
#elif defined(_DIRECTIONAL_PCF7)
	#define DIRECTIONAL_FILTER_SAMPLES 16
	#define DIRECTIONAL_FILTER_SETUP SampleShadow_ComputeSamples_Tent_7x7
#endif
```

创建一个新的 FilterDirectionalShadow 函数，用于阴影瓦片空间位置。当定义了 DIRECTIONAL_FILTER_SETUP 时，它需要进行多次采样，否则可以简单地调用一次 SampleDirectionalShadowAtlas 就足够了。

```
float FilterDirectionalShadow (float3 positionSTS) {
	#if defined(DIRECTIONAL_FILTER_SETUP)
		float shadow = 0;
		return shadow;
	#else
		return SampleDirectionalShadowAtlas(positionSTS);
	#endif
}
```

滤波器设置函数有四个参数。首先是一个 float4，其中前两个分量是 X 和 Y 纹素大小，后两个分量是总纹理大小。然后是原始采样位置，接下来是每个采样的权重和位置的输出参数，它们分别定义为 float 和 float2 数组。之后，我们可以循环遍历所有采样点，按其权重进行累积调制。

```
	#if defined(DIRECTIONAL_FILTER_SETUP)
		float weights[DIRECTIONAL_FILTER_SAMPLES];
		float2 positions[DIRECTIONAL_FILTER_SAMPLES];
		float4 size = _ShadowAtlasSize.yyxx;
		DIRECTIONAL_FILTER_SETUP(size, positionSTS.xy, weights, positions);
		float shadow = 0;
		for (int i = 0; i < DIRECTIONAL_FILTER_SAMPLES; i++) {
			shadow += weights[i] * SampleDirectionalShadowAtlas(
				float3(positions[i].xy, positionSTS.z)
			);
		}
		return shadow;
	#else
```

在 GetDirectionalShadowAttenuation 中调用这个新函数，而不是直接调用 SampleDirectionalShadowAtlas。

```
	float shadow = FilterDirectionalShadow(positionSTS);
	return lerp(1.0, shadow, directional.strength);
```

增加滤波器大小会使阴影更加平滑，但也会导致阴影痤疮再次出现。我们必须增加法线偏移以匹配滤波器大小。在 SetCascadeData 中，我们可以通过将纹素大小乘以一加上滤波模式来自动完成这个操作。

```
	void SetCascadeData (int index, Vector4 cullingSphere, float tileSize) {
		float texelSize = 2f * cullingSphere.w / tileSize;
		float filterSize = texelSize * ((float)settings.directional.filter + 1f);
		…
			1f / cullingSphere.w,
			filterSize * 1.4142136f
		);
	}
```

此外，增加采样区域意味着我们可能会在级联的剔除球之外进行采样。我们可以通过在将其平方之前减小球的半径以匹配滤波器大小来避免这种情况。

```
		cullingSphere.w -= filterSize;
		cullingSphere.w *= cullingSphere.w;
```

解决了shadow acne，但filter size增加又会带来wall shadow的问题，所以需要调整bias或者使用更大尺寸的Atlas来解决这个问题

### Blending Cascades

更柔和的阴影看起来更好，但它们也会使级联之间的突然过渡更加明显。

我们可以通过在级联之间添加一个过渡区域来使过渡变得不那么明显，虽然不能完全隐藏。

```
struct ShadowData {
	int cascadeIndex;
	float cascadeBlend;
	float strength;
};
```

首先，在Shadows的ShadowData中添加一个级联混合值，我们将使用它来在相邻的级联之间进行插值。

在GetShadowData中最初将混合值设置为1，表示所选的级联具有完全的强度。然后，每当在循环中找到级联时，始终计算淡化因子。如果我们在最后一个级联，像以前一样将其因子与强度相乘，否则将其用于混合。

```
	data.cascadeBlend = 1.0;
	data.strength = FadedShadowStrength(
		surfaceWS.depth, _ShadowDistanceFade.x, _ShadowDistanceFade.y
	);
	int i;
	for (i = 0; i < _CascadeCount; i++) {
		float4 sphere = _CascadeCullingSpheres[i];
		float distanceSqr = DistanceSquared(surfaceWS.position, sphere.xyz);
		if (distanceSqr < sphere.w) {
			float fade = FadedShadowStrength(
				distanceSqr, _CascadeData[i].x, _ShadowDistanceFade.z
			);
			if (i == _CascadeCount - 1) {
				data.strength *= fade;
			}
			else {
				data.cascadeBlend = fade;
			}
			break;
		}
	}
```

现在在GetDirectionalShadowAttenuation中检查级联混合值是否在检索到第一个阴影值后小于1。如果是这样，我们就在一个过渡区域中，必须同时从下一个级联中采样，并在两个值之间进行插值。

```
	float shadow = FilterDirectionalShadow(positionSTS);
	if (global.cascadeBlend < 1.0) {
		normalBias = surfaceWS.normal *
			(directional.normalBias * _CascadeData[global.cascadeIndex + 1].y);
		positionSTS = mul(
			_DirectionalShadowMatrices[directional.tileIndex + 1],
			float4(surfaceWS.position + normalBias, 1.0)
		).xyz;
		shadow = lerp(
			FilterDirectionalShadow(positionSTS), shadow, global.cascadeBlend
		);
	}
	return lerp(1.0, shadow, directional.strength);
```

### Dithered Transition


虽然级联之间的混合看起来更好，但它也使我们在混合区域必须两倍采样阴影贴图的次数。另一种替代方法是始终从一个级联中进行采样，基于一个抖动模式。这可能看起来不够好，但成本要低得多，尤其在使用大型滤波器时。

在 Directional 中添加级联混合模式选项，支持硬混合、软混合或抖动方法。

```
		public enum CascadeBlendMode {
			Hard, Soft, Dither
		}

		public CascadeBlendMode cascadeBlend;
	}

	public Directional directional = new Directional {
		…
		cascadeFade = 0.1f,
		cascadeBlend = Directional.CascadeBlendMode.Hard
	};
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/cascade-blend-mode.png)

Add a static array for soft and dither cascade blending keywords to `**Shadows**`.

```
	static string[] cascadeBlendKeywords = {
		"_CASCADE_BLEND_SOFT",
		"_CASCADE_BLEND_DITHER"
	};
```

调整 SetKeywords 以使其适用于任意的关键字数组和索引，然后设置级联混合关键字。

```
	void RenderDirectionalShadows () {
		SetKeywords(
			directionalFilterKeywords, (int)settings.directional.filter - 1
		);
		SetKeywords(
			cascadeBlendKeywords, (int)settings.directional.cascadeBlend - 1
		);
		buffer.SetGlobalVector(
			shadowAtlasSizeId, new Vector4(atlasSize, 1f / atlasSize)
		);
		buffer.EndSample(bufferName);
		ExecuteBuffer();
	}

	void SetKeywords (string[] keywords, int enabledIndex) {
		//int enabledIndex = (int)settings.directional.filter - 1;
		for (int i = 0; i < keywords.Length; i++) {
			if (i == enabledIndex) {
				buffer.EnableShaderKeyword(keywords[i]);
			}
			else {
				buffer.DisableShaderKeyword(keywords[i]);
			}
		}
	}
```

Add the required multi-compile direction to the *CustomLit* pass.

```
	#pragma multi_compile _ _CASCADE_BLEND_SOFT _CASCADE_BLEND_DITHER
			#pragma multi_compile_instancing
```

To perform dithering we need a dither `**float**` value, which we can add to `**Surface**`.

```
struct Surface {
	…
	float dither;
};
```

在 LitPassFragment 中生成抖动值有多种方法。最简单的方法是使用核心 RP 库中的 InterleavedGradientNoise 函数，它根据屏幕空间的 XY 位置生成一个旋转平铺的抖动模式。在片段函数中，这等同于剪切空间的 XY 位置。它还需要第二个参数用于动画，但我们不需要它，可以将其保留为零。

```
	surface.smoothness =
		UNITY_ACCESS_INSTANCED_PROP(UnityPerMaterial, _Smoothness);
	surface.dither = InterleavedGradientNoise(input.positionCS.xy, 0);
```

在 GetShadowData 中设置级联索引之前，当不使用软混合时将级联混合设置为零。这样整个分支将从这些着色器变种中被消除。

```
	if (i == _CascadeCount) {
		data.strength = 0.0;
	}
	#if !defined(_CASCADE_BLEND_SOFT)
		data.cascadeBlend = 1.0;
	#endif
	data.cascadeIndex = i;
```

而当使用抖动混合时，如果我们不在最后一个级联中，并且混合值小于抖动值，那么跳到下一个级联。

```
	if (i == _CascadeCount) {
		data.strength = 0.0;
	}
	#if defined(_CASCADE_BLEND_DITHER)
		else if (data.cascadeBlend < surfaceWS.dither) {
			i += 1;
		}
	#endif
	#if !defined(_CASCADE_BLEND_SOFT)
		data.cascadeBlend = 1.0;
	#endif
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/dithered-cascades.png)

抖动混合的可接受程度取决于我们渲染帧的分辨率。如果使用了后处理效果来模糊最终结果，那么它可以非常有效，例如与时域抗锯齿和动画抖动模式相结合使用。

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/dithered-zoomed-in.png)

### Culling Bias

使用级联阴影贴图的一个不足之处是，我们可能会多次渲染同一个投射阴影的物体，这在每个光源中都会发生。如果可以保证它们的结果始终会被较小的级联所覆盖，那么从较大的级联中剔除一些投射阴影的物体是有道理的。在应用到阴影设置之前，在 RenderDirectionalShadows 中将分裂数据的 shadowCascadeBlendCullingFactor 设置为一，Unity 可以实现这一点。

```
splitData.shadowCascadeBlendCullingFactor = 1f;
			shadowSettings.splitData = splitData;
```

![0](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/culling-bias-0.png)

![1](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/shadow-quality/culling-bias-1.png)

这个值是一个因子，用于调制用于执行剔除的前一个级联的半径。Unity在剔除时相当保守，但我们应该将其减小，以减去级联淡化比例和一点额外的值，以确保在过渡区域中的投射阴影物体不会被剔除。因此，让我们使用 0.8 减去淡化范围，最小值为零。如果您看到在级联过渡周围的阴影中出现空洞，那么必须进一步减小这个值。

```
		float cullingFactor =
			Mathf.Max(0f, 0.8f - settings.directional.cascadeFade);
		
		for (int i = 0; i < cascadeCount; i++) {
			…
			splitData.shadowCascadeBlendCullingFactor = cullingFactor;
			…
		}
```

## Transparency

我们将通过考虑透明阴影投射物来结束本教程。剪裁、淡化和透明材质都可以像不透明材质一样接收阴影，但只有剪裁材质目前能够正确地投射阴影。透明物体表现得好像它们是实体阴影投射者一样。

![clipped](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/transparency/clipped.png)

![transparent](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/transparency/transparent.png)

### Shadow Modes

我们可以以几种方式修改阴影投射者。由于它涉及写入深度缓冲区，所以我们的阴影是二进制的，要么存在，要么不存在，但这仍然给了我们一些灵活性。它们可以被打开并且完全不透明，剪裁，抖动，或完全关闭。我们可以独立于其他材质属性进行这些修改，以支持最大的灵活性。因此，让我们为此添加一个单独的 _Shadows 着色器属性。我们可以使用 KeywordEnum 属性为其创建一个关键字下拉菜单，其中默认打开阴影。

```
	[KeywordEnum(On, Clip, Dither, Off)] _Shadows ("Shadows", Float) = 0
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/transparency/shadow-modes.png)

为这些模式添加一个着色器特性，以替代现有的 _CLIPPING 特性。我们只需要三种变种，使用无关键字表示开启和关闭，使用 _SHADOWS_CLIP 和 _SHADOWS_DITHER。

```
			//#pragma shader_feature _CLIPPING
			#pragma shader_feature _ _SHADOWS_CLIP _SHADOWS_DITHER
```

Create a setter property for shadows in `**CustomShaderGUI**`.

```
	enum ShadowMode {
		On, Clip, Dither, Off
	}

	ShadowMode Shadows {
		set {
			if (SetProperty("_Shadows", (float)value)) {
				SetKeyword("_SHADOWS_CLIP", value == ShadowMode.Clip);
				SetKeyword("_SHADOWS_DITHER", value == ShadowMode.Dither);
			}
		}
	}
```

然后在预设方法中相应地设置阴影。不透明的情况下开启，剪裁的情况下使用剪裁，淡化和透明的情况下都使用抖动。

### Clipped Shadows

In `ShadowCasterPassFragment`, replace the check for *_CLIPPED* with one for *_SHADOWS_CLIP*.

```
	#if defined(_SHADOWS_CLIP)
		clip(base.a - UNITY_ACCESS_INSTANCED_PROP(UnityPerMaterial, _Cutoff));
	#endif
```

现在可以为透明材质提供剪裁的阴影，这对于具有大部分完全不透明或完全透明部分但需要使用 alpha 混合的表面可能是合适的。

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/transparency/transparent-clipped-shadows.png)

请注意，剪裁阴影不像完全不透明的阴影那样稳定，因为当视图移动时，阴影矩阵会发生变化，导致片段略微移动。这可能会导致阴影贴图的一个纹素突然从剪裁变为未剪裁。

### Dithered Shadows

抖动阴影的工作原理与剪裁阴影类似，只是判定的标准不同。在这种情况下，我们从表面 alpha 值中减去一个抖动值，然后基于这个值进行剪裁。我们可以再次使用 InterleavedGradientNoise 函数来实现这个目标。

```
	#if defined(_SHADOWS_CLIP)
		clip(base.a - UNITY_ACCESS_INSTANCED_PROP(UnityPerMaterial, _Cutoff));
	#elif defined(_SHADOWS_DITHER)
		float dither = InterleavedGradientNoise(input.positionCS.xy, 0);
		clip(base.a - dither);
	#endif
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/transparency/dithered-shadows.png)

抖动可以用来近似半透明的阴影投射者，但这是一种相当粗糙的方法。硬抖动阴影看起来不太好，但当使用较大的 PCF 滤波器时可能看起来是可以接受的。

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/transparency/dithered-pcf7.png)

因为抖动模式是固定的，每个纹素之间重叠的半透明阴影投射者不会投射出更加深色的合并阴影。效果与最不透明的阴影投射者一样强烈。此外，由于生成的模式噪声较大，当阴影矩阵变化时，它更容易受到时间相关的伪影的影响，可能使阴影看起来颤动不定。这种方法对于具有固定投影的其他光源类型效果更好，只要物体不移动。通常，对于半透明对象，更实际的方法是要么使用剪裁阴影，要么完全不使用阴影。

### No Shadows


可以通过调整物体的 MeshRenderer 组件的 "Cast Shadows" 设置来关闭单个物体的阴影投射。然而，如果您希望禁用使用相同材质的所有物体的阴影，那么我们还将支持按材质禁用阴影。我们可以通过禁用材质的 ShadowCaster 通道来实现这一点。

在 CustomShaderGUI 中添加一个 SetShadowCasterPass 方法，首先检查 _Shadows 着色器属性是否存在。如果存在，还要检查所有选定的材质是否已设置为相同的模式，通过其 hasMixedValue 属性来判断。如果没有模式或模式混合，则中止操作。否则，通过调用 SetShaderPassEnabled，在所有材质上启用或禁用 ShadowCaster 通道，将通道名称和启用状态作为参数传递。

```
	void SetShadowCasterPass () {
		MaterialProperty shadows = FindProperty("_Shadows", properties, false);
		if (shadows == null || shadows.hasMixedValue) {
			return;
		}
		bool enabled = shadows.floatValue < (float)ShadowMode.Off;
		foreach (Material m in materials) {
			m.SetShaderPassEnabled("ShadowCaster", enabled);
		}
	}
```

确保通道正确设置的最简单方法是在通过 GUI 更改材质时始终调用 SetShadowCasterPass。我们可以在 OnGUI 的开始使用 EditorGUI.BeginChangeCheck，并在其结束时使用 EditorGUI.EndChangeCheck。后者方法返回自从开始检查以来是否有变化。如果有变化，就设置阴影投射通道。

```
	public override void OnGUI (
		MaterialEditor materialEditor, MaterialProperty[] properties
	) {
		EditorGUI.BeginChangeCheck();
		…
		if (EditorGUI.EndChangeCheck()) {
			SetShadowCasterPass();
		}
	}
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/transparency/not-casting-shadows.png)

### Unlit Shadow Casters

尽管不受光照影响的材质不会受到光照的影响，但您可能希望它们投射阴影。我们可以通过简单地将 Lit 着色器中的 ShadowCaster 通道复制到 Unlit 着色器中来实现这一点。

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/transparency/unlit-shadow-casters.png)

### Receiving Shadows

最后，我们还可以使受光照影响的表面忽略阴影，这对于像全息影像这样的应用或者纯粹的艺术目的可能会很有用。为此，在 Lit 着色器中添加一个 _RECEIVE_SHADOWS 关键字开关属性。

```
		[Toggle(_RECEIVE_SHADOWS)] _ReceiveShadows ("Receive Shadows", Float) = 1
```

还需要在 CustomLit 通道中添加相应的着色器特性来支持这个功能。

```
		#pragma shader_feature _RECEIVE_SHADOWS
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/transparency/receiving-shadows.png)

当定义了该关键字时，我们只需在 GetDirectionalShadowAttenuation 中将阴影衰减强制设为1。

```
float GetDirectionalShadowAttenuation (…) {
	#if !defined(_RECEIVE_SHADOWS)
		return 1.0;
	#endif
	…
}
```

![img](https://catlikecoding.com/unity/tutorials/custom-srp/directional-shadows/transparency/not-receiving-shadows.png)